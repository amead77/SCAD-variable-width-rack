include <330mm rack defines.scad>;

/*
// next 2 lines used only by my 'on save' script. can be ignored otherwise.
// AUTO-V
version = "v0.1-2026/05/04r66";
*/




// post(slide_side, doublewide)
// Builds a single 1U post body with optional slide channel(s).
// slide_side: 0=none, 1=left, 2=right, 3=both. doublewide: 0=single, 1=double.
// e.g. post(slide_side=1, doublewide=0);
module post(slide_side = 0, doublewide = 0) {
    union() {
        cube([post_width, post_width, u_height]);
        if (doublewide == 1) {
            translate([post_width, 0, 0]) {
                cube([post_width, post_width, u_height]);
            }
        }
        if (doublewide == 0) {
            if ((slide_side == 1) || (slide_side == 3)) {
                post_slider_left();
            } 
            if ((slide_side == 2) || (slide_side == 3)) {
                post_slider_right();
            }
        } else {
            // doublewide
            if ((slide_side == 1) || (slide_side == 3)) {
                // Left sliders on both posts
                post_slider_left();
                translate([post_width, 0, 0]) {
                    post_slider_left();
                }
            } 
            if ((slide_side == 2) || (slide_side == 3)) {
                // Right sliders on both posts
                post_slider_right();
                translate([post_width, 0, 0]) {
                    post_slider_right();
                }
            } 
 
        }
    }
}


// post_slides()
// Internal helper — generates the slide rail geometry that attaches to a post side.
// Not called directly; used by post_slider_left() and post_slider_right().
module post_slides() {
    difference() {
        cube([post_slide_width, post_width, u_height]);    
        translate([0, 0, (hole_offset_z/2)-(post_slide_cutout/2)]) {
            cube([post_slide_cutout, post_width, post_slide_cutout]);
        }
        translate([0, 0, (hole_offset_z/2)-(post_slide_cutout/2)+ hole_spacing]) {
            cube([post_slide_cutout, post_width, post_slide_cutout]);
        }
        translate([0, 0, (hole_offset_z/2)-(post_slide_cutout/2)+ hole_spacing * 2]) {
            cube([post_slide_cutout, post_width, post_slide_cutout]);
        }
    }
}

// post_slider_left()
// Internal helper — attaches a slide rail to the left side of a post.
module post_slider_left() {
    translate([post_width, 0, 0]) {
        post_slides();
    }
}


// post_slider_right()
// Internal helper — attaches a slide rail to the right side of a post.
module post_slider_right() {
    translate([-post_slide_width, 0, 0]) {
        post_slides();
    }
}

// rail_1u_holes_segment(slide_side, doublewide)
// Internal helper — one 1U post segment with screw holes and nut traps subtracted.
// slide_side: 0=none, 1=left, 2=right, 3=both. doublewide: 0=single, 1=double.
module rail_1u_holes_segment(slide_side, doublewide = 0) {
// i've bashed this together rather than math it.

    difference() {
        post(slide_side, doublewide);
        holes(holes = 3); //1u sections have 3 holes per standard.
        nut_holes(holes = 3);
        if (doublewide == 1) {
            translate([post_width, 0, 0]) {
                holes(holes = 3); //1u sections have 3 holes per standard.
                nut_holes(holes = 3);
            }
        }
    }
}


// footer_body(doublewide)
// Internal helper — solid block at the bottom of the post for the footer attachment.
// doublewide: 0=single, 1=double.
module footer_body(doublewide = 0) {
    translate([0, 0, -footer_height]) {
        cube([post_width, post_width, footer_height]);
    }
    if (doublewide == 1) {
        translate([post_width, 0, -footer_height]) {
            cube([post_width, post_width, footer_height]);
        }
    }
}

// footer_hole_and_nut()
// Internal helper — subtracts the screw hole and nut trap from the footer body.
module footer_hole_and_nut() {
    // Place one footer hole 0.5U below the first post hole center.
    translate([post_width/2, ((post_width)-post_width/2 ), -(hole_offset_z/2)]) {
        rotate([90,0,0]) {
            cylinder(d=hole_d, h=post_width, center=true, $fn=32);
        }
    }
    translate([post_width/2, ((post_width)-nut_thickness/2 ), -(hole_offset_z/2)]) {
        rotate([90,0,0]) {
            cylinder(d=nut_diameter_point, h=nut_thickness, center=true, $fn=6);
        }
    }
}

// header_body(post_height, doublewide)
// Internal helper — solid block at the top of the post for the header attachment.
// post_height: number of U units. doublewide: 0=single, 1=double.
module header_body(post_height, doublewide = 0) {
    translate([0, 0, u_height * post_height]) {
        cube([post_width, post_width, header_height]);
    }
    if (doublewide == 1) {
        translate([post_width, 0, u_height * post_height]) {
            cube([post_width, post_width, header_height]);
        }
    }
}

// header_hole_and_nut(post_height)
// Internal helper — subtracts the screw hole and nut trap from the header body.
// post_height: number of U units the post is tall.
module header_hole_and_nut(post_height) {
    // Place one header hole 0.5U above the top of the posts.
    translate([post_width/2, ((post_width)-post_width/2 ), u_height * post_height + (hole_offset_z/2)]) {
        rotate([90,0,0]) {
            cylinder(d=hole_d, h=post_width, center=true, $fn=32);
        }
    }
    translate([post_width/2, ((post_width)-nut_thickness/2 ), u_height * post_height + (hole_offset_z/2)]) {
        rotate([90,0,0]) {
            cylinder(d=nut_diameter_point, h=nut_thickness, center=true, $fn=6);
        }
    }
}


// base_joiner_hole_and_nut()
// Internal helper — subtracts the screw hole and nut trap from a joiner block.
module base_joiner_hole_and_nut() {
    // Place one footer hole 0.5U below the first post hole center.
    translate([post_width/2, ((post_width)-post_width/2 ), (hole_offset_z/2)]) {
        rotate([90,0,0]) {
            cylinder(d=hole_d, h=post_width, center=true, $fn=32);
        }
    }
    translate([post_width/2, ((post_width)-nut_thickness/2 ), (hole_offset_z/2)]) {
        rotate([90,0,0]) {
            cylinder(d=nut_diameter_point, h=nut_thickness, center=true, $fn=6);
        }
    }
}

// base_joiner_block(doublewide)
// Internal helper — one joiner mounting block with hole and nut trap.
// doublewide: 0=single, 1=double.
module base_joiner_block(doublewide = 0) {
    translate([0, 0, -(footer_height*2)]) {
        difference() {
            cube([post_width, post_width, footer_height]);
            base_joiner_hole_and_nut();
        }
    }
    if (doublewide == 1) {
        translate([post_width, 0, -(footer_height*2)]) {
            difference() {
                cube([post_width, post_width, footer_height]);
                base_joiner_hole_and_nut();
            }
        }
    }

}

// base_joiner_core(doublewide, bottom, supports, beam_thickness)
// Internal helper — builds the full joiner body (blocks + beam + alignment cones).
// doublewide: 0=single, 1=double. bottom: 1=base joiner, 0=top joiner.
// supports: minimum 2 (front + rear); >2 adds equally-spaced intermediate support blocks.
// beam_thickness: override the beam thickness; defaults to footer_base_beam_thickness or header_top_beam_thickness.
module base_joiner_core(doublewide = 0, bottom = 1, supports = 2, beam_thickness = 5.0) {
    //_beam_thickness = (beam_thickness >= 0) ? beam_thickness : ((bottom == 1) ? footer_base_beam_thickness : header_top_beam_thickness);
    _beam_thickness = beam_thickness;
    union() {
        base_joiner_block(doublewide);
        translate([0, rack_width-post_width, 0]) {
            if (doublewide == 1) {
                translate([post_width*2, post_width, 0]) {
                    rotate([0,0,180]) {
                            base_joiner_block(doublewide);
                    }
                }
            } else {
                translate([post_width, post_width, 0]) {
                    rotate([0,0,180]) {
                        base_joiner_block(doublewide);
                    }
                }
            }
        }
        if (doublewide == 1) {
            translate([0/*-post_width*/, 0, 0]) {
                base_joiner_block(doublewide);
            }
        } else {
            base_joiner_block(doublewide);
        }
        // Intermediate support blocks, equally spaced between front and rear
        if (supports > 2) {
            for (i = [1:supports-2]) {
                translate([0, i * (rack_width - post_width) / (supports - 1), 0]) {
                    base_joiner_block(doublewide);
                }
            }
        }
        translate([0, 0, -((footer_height * 2)+_beam_thickness)]) {
            if (doublewide == 0) {
                cube([post_width, rack_width, _beam_thickness]);
            }
            if (doublewide == 1) {
                cube([post_width * 2, rack_width, _beam_thickness]);
            }
        }

        // Top cones on front joiner block(s) — mate with footer bottom cone sockets
        if (bottom == 1) {
            translate([post_width/2, post_width/2, -footer_height]) {
                cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
            }
            if (doublewide == 1) {
                translate([post_width + post_width/2, post_width/2, -footer_height]) {
                    cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                }
            }

            // Top cones on rear joiner block(s)
            translate([post_width/2, rack_width - post_width/2, -footer_height]) {
                cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
            }
            if (doublewide == 1) {
                translate([post_width + post_width/2, rack_width - post_width/2, -footer_height]) {
                    cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                }
            }

            // Top cones on intermediate support block(s)
            if (supports > 2) {
                for (i = [1:supports-2]) {
                    translate([post_width/2, i * (rack_width - post_width) / (supports - 1) + post_width/2, -footer_height]) {
                        cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                    }
                    if (doublewide == 1) {
                        translate([post_width + post_width/2, i * (rack_width - post_width) / (supports - 1) + post_width/2, -footer_height]) {
                            cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                        }
                    }
                }
            }
        }    
    }
}

// base_joiner(doublewide, bottom, supports, beam_thickness)
// Horizontal joiner that connects a front and rear post pair via the footer/header.
// doublewide: 0=single, 1=double. bottom: 1=base/footer joiner (default), 0=top/header joiner.
// supports: min 2 (front + rear end blocks); >2 adds equally-spaced intermediate tray support blocks.
// beam_thickness: override beam thickness; defaults to footer_base_beam_thickness or header_top_beam_thickness.
// e.g. base_joiner(doublewide=0);                           // base joiner, single wide
// e.g. base_joiner(doublewide=0, bottom=0);                 // top joiner, single wide
// e.g. base_joiner(doublewide=0, supports=4);               // base joiner with 2 extra intermediate supports
// e.g. base_joiner(doublewide=0, beam_thickness=8);         // base joiner with custom beam thickness
module base_joiner(doublewide = 0, bottom = 1, supports = 2, beam_thickness = 5.0) {
    if (bottom == 1) {
        base_joiner_core(doublewide, bottom, supports, beam_thickness);
    } else {
        // Top joiner: flip Z and invert cones to sockets so header top cones lock in.
        difference() {
            mirror([0, 0, 1]) {
                base_joiner_core(doublewide, bottom, supports, beam_thickness);
            }

            // Front socket(s)
            translate([post_width/2, post_width/2, header_height]) {
                cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
            }
            if (doublewide == 1) {
                translate([post_width + post_width/2, post_width/2, header_height]) {
                    cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                }
            }

            // Rear socket(s)
            translate([post_width/2, rack_width - post_width/2, header_height]) {
                cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
            }
            if (doublewide == 1) {
                translate([post_width + post_width/2, rack_width - post_width/2, header_height]) {
                    cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                }
            }

            // Intermediate socket(s)
            if (supports > 2) {
                for (i = [1:supports-2]) {
                    translate([post_width/2, i * (rack_width - post_width) / (supports - 1) + post_width/2, header_height]) {
                        cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                    }
                    if (doublewide == 1) {
                        translate([post_width + post_width/2, i * (rack_width - post_width) / (supports - 1) + post_width/2, header_height]) {
                            cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                        }
                    }
                }
            }
        }
    }
}


// rail_1u_holes(slide_side, doublewide, post_height, v_post_cones, include_footer, include_header)
// Main public module — generates a complete post of post_height U units with holes, nut traps,
// optional slide rails, optional footer/header blocks, and optional alignment cones.
// slide_side: 0=none, 1=left, 2=right, 3=both.
// doublewide: 0=single, 1=double.
// post_height: number of U units (default v_post_height from defines).
// v_post_cones: 1=add alignment cones for stacking (default post_cones from defines).
// include_footer/include_header: 1=include attachment block (default from defines).
// e.g. rail_1u_holes(slide_side=1, doublewide=0, post_height=6, v_post_cones=1);
module rail_1u_holes(slide_side, doublewide = 0, post_height = v_post_height, v_post_cones = post_cones, include_footer = footer_include, include_header = header_include) {
    difference()  {
        union() {
            for (z = [0:post_height-1]) {
                translate([0,0,z*u_height]) {
                    rail_1u_holes_segment(slide_side, doublewide);
                }
            }
            if (include_footer == 1) {
                footer_body(doublewide);
            }
            if (include_header == 1) {
                header_body(post_height, doublewide);
            }
            if (v_post_cones == 1) { //top cones for joining rails
                if (include_header == 1) {
                    translate([post_width/2, post_width/2, u_height * post_height + header_height]) {
                        cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                    }
                    if (doublewide == 1) {
                        translate([post_width + post_width/2, post_width/2, u_height * post_height + header_height]) {
                            cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                        }
                    }
                } else {
                    translate([post_width/2, post_width/2, u_height * post_height]) {
                        rotate([0,0,0]) {
                            cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                        }
                    }
                    if (doublewide == 1) {
                        translate([post_width + post_width/2, post_width/2, u_height * post_height]) {
                            rotate([0,0,0]) {
                                cylinder(h=post_cone_height, r1=(post_cone_base_diameter/2)-post_top_cone_clearance, r2=(post_cone_top_diameter/2)-post_top_cone_clearance, center=false, $fn=32);
                            }
                        }
                    }
                }
            }
        }

        if (include_header == 1) {
            header_hole_and_nut(post_height);
            if (doublewide == 1) {
                translate([post_width, 0, 0]) {
                    header_hole_and_nut(post_height);
                }
            }
        }

        if (include_footer == 1) {
            footer_hole_and_nut();
            if (doublewide == 1) {
                translate([post_width, 0, 0]) {
                    footer_hole_and_nut();
                }
            }
        }

        if (v_post_cones == 1) { //bottom cones
            if (include_footer == 1) {
                translate([post_width/2, post_width/2, -footer_height]) {
                    cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                }
                if (doublewide == 1) {
                    translate([post_width + post_width/2, post_width/2, -footer_height]) {
                        cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                    }
                }
            } else {
                translate([post_width/2, post_width/2, 0]) {
                    cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                }
                if (doublewide == 1) {
                    translate([post_width + post_width/2, post_width/2, 0]) {
                        cylinder(h=post_cone_height-post_top_cone_clearance, r1=(post_cone_base_diameter/2), r2=(post_cone_top_diameter/2), center=false, $fn=32);
                    }
                }
            }
        }
    }
}

// holes(holes)
// Internal helper — subtracts M6 screw holes through a post face. holes: 2 or 3 per 1U segment.
module holes(holes = 2) {
    translate([post_width/2, ((post_width)-post_width/2 ), hole_offset_z/2]) {
        rotate([90,0,0]) {
            cylinder(d=hole_d, h=post_width, center=true, $fn=32);
        }
    }

    if (holes == 3 ) {
        translate([post_width/2, ((post_width)-post_width/2 ), (hole_offset_z/2) + hole_spacing]) {
            rotate([90,0,0]) {
                cylinder(d=hole_d, h=post_width, center=true, $fn=32);
            }
        }
    }

    translate([post_width/2, ((post_width)-post_width/2 ), (hole_offset_z/2)+ (hole_spacing*2)]) {
        rotate([90,0,0]) {
            cylinder(d=hole_d, h=post_width, center=true, $fn=32);
        }
    }
}


// nut_holes(holes)
// Internal helper — subtracts M6 hex nut traps from the rear of a post face. holes: 2 or 3.
module nut_holes(holes = 2) {

        translate([post_width/2, ((post_width)-nut_thickness/2 ), hole_offset_z/2]) {
            rotate([90,0,0]) {
                cylinder(d=nut_diameter_point, h=nut_thickness, center=true, $fn=6);
            }
        }

        translate([post_width/2, ((post_width)-nut_thickness/2 ), (hole_offset_z/2) + hole_spacing]) {
            rotate([90,0,0]) {
                cylinder(d=nut_diameter_point, h=nut_thickness, center=true, $fn=6);
            }
        }

        translate([post_width/2, ((post_width)-nut_thickness/2 ), (hole_offset_z/2)+ (hole_spacing*2)]) {
            rotate([90,0,0]) {
                cylinder(d=nut_diameter_point, h=nut_thickness, center=true, $fn=6);
            }
        }

}